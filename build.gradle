apply plugin: 'java'
apply plugin: 'maven'

version = '1.1.4'
group = 'net.chris54721'
archivesBaseName = 'openmcauthenticator'
sourceCompatibility = 1.6

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-ftp', version: '2.10'
}

tasks.withType(Upload) {
    doFirst {
        if (!project.hasProperty("mavenUser")) {
            throw new InvalidUserDataException("The mavenUser property is not defined. Failed deploying to Maven.")
        }
        if (!project.hasProperty("mavenPassword")) {
            throw new InvalidUserDataException("The mavenPassword property is not defined. Failed deploying to Maven.")
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}

if (project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "ftp://chris54721.net") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
        }
    }
}
