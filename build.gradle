apply plugin: 'java'
apply plugin: 'maven-publish'

version = '1.1.1'
group = 'net.chris54721'
archivesBaseName = 'openmcauthenticator'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
}

uploadArchives << {
    if(!project.hasProperty("mavenUser")) {
        throw new InvalidUserDataException("The mavenUser property is not defined. Failed deploying to Maven.")
    }
    if(!project.hasProperty("mavenPassword")) {
        throw new InvalidUserDataException("The mavenPassword property is not defined. Failed deploying to Maven.")
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

if(project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId 'openmcauthenticator'
                from components.java
                artifact (javadocJar) {
                    classifier = 'javadoc'
                }
                artifact (sourceJar) {
                    classifier = 'sources'
                }
            }
        }
        repositories {
            maven {
                url 'sftp://chris54721.net:22/repo'
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
            }
        }
    }
}